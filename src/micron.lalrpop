use std::str::FromStr;
use crate::ast::{Expr, Opcode, UnaryOpcode, Statement, FLOAT_PRECISION, StringExpr}; // (0)
use rug;
use rug::Assign;

grammar;

pub Program: Vec<Box<Statement>> = {

    // Accept multiple statements
    Statements+ => <>
}

// All of the types of statements within the language
Statements: Box<Statement> = {

    <a:AssignmentStatement> => a,
    <b:BareExpressionStatement> => b
}

// Assignment statement 
AssignmentStatement: Box<Statement> = {

    "let" <v:Variable> "=" <e:Expr> ";"   => Box::new(Statement::Assignment(v,e)),
    "let" <v:Variable> "=" <s:Sexpr> ";" => Box::new(Statement::StringAssignment(v,s))
}

// Bare Expression
BareExpressionStatement: Box<Statement> = {

    <e:Expr> => Box::new(Statement::BareExpression(e))
}

// String Expression
Sexpr: Box<StringExpr> = {
    String => Box::new(StringExpr::String(<>))
}

// Expression
Expr: Box<Expr> = {
    Expr ExprOp Factor => Box::new(Expr::Op(<>)),
    Factor,
    "!" <f:Factor> => Box::new(Expr::UnaryOp(f, UnaryOpcode::Negate)),
    "~" <f:Factor> => Box::new(Expr::UnaryOp(f, UnaryOpcode::BwNot)),
};

ExprOp: Opcode = { // (3)
    "+"  => Opcode::Add,
    "-"  => Opcode::Sub,
    "<=" => Opcode::Lte,
    ">=" => Opcode::Gte,
    ">"  => Opcode::Gt,
    "<"  => Opcode::Lt,
    "==" => Opcode::Equal
};

Factor: Box<Expr> = {
    Factor FactorOp Term => Box::new(Expr::Op(<>)),
    Term,
};

FactorOp: Opcode = {
    "*"   => Opcode::Mul,
    "/"   => Opcode::Div,
    "**" => Opcode::Pow,
    "%"   => Opcode::Mod,
    "<<"  => Opcode::Lsh,
    ">>"  => Opcode::Rsh,
    "^"   => Opcode::BwXor,
    "|"   => Opcode::BwOr,
    "&"   => Opcode::BwAnd,
    "||"  => Opcode::Or,
    "&&"  => Opcode::And
};

Term: Box<Expr> = {
    Integer => Box::new(Expr::Number(<>)),
    Float   => Box::new(Expr::Real(<>)),
    Variable => Box::new(Expr::Variable(<>)),
    "(" <Expr> ")",
};

Integer: rug::Integer = {
    r"(\-)?[0-9]+" => {

        let mut value = rug::Integer::new();
        value.assign(rug::Integer::parse(<>).unwrap());
        value
    }
};

Float: rug::Float = {
    r"(\-)?[0-9]+\.[0-9]+" => {

        let mut value = rug::Float::new(FLOAT_PRECISION);
        value.assign(rug::Float::parse(<>).unwrap());
        value
    }
}

String: String = {
    r#""([^"\\]|\\[0nrt"\\])*""# => String::from(<>)
}

Variable: String = {
    r"[a-zA-Z_]+([0-9]+)?" => String::from_str(<>).unwrap()
};